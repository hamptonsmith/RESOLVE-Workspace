Concept Preemptable_Queue_Template(type Entry; evaluates Max_Length: Integer);
    uses String_Theory;
    requires Max_Length > 0;
 
    Type Family P_Queue is modeled by Str(Entry);
    exemplar Q;
    constraint |Q| <= Max_Length;
    initialization ensures Q = Empty_String;
 
    Operation Enqueue(alters E: Entry; updates Q: P_Queue);
        requires |Q| < Max_Length;
        ensures Q = #Q o <#E>;
 
    Operation Inject(alters E: Entry; updates Q: P_Queue);
        requires |Q| < Max_Length;
        ensures Q = <#E> o #Q;

    Operation Dequeue(replaces R: Entry; updates Q: P_Queue);
        requires |Q| /= 0;
        ensures #Q = <R> o Q;

    Operation Swap_Last_Entry(updates E: Entry; updates Q: P_Queue);
        requires |Q| /= 0;
        ensures Q = Substring(#Q, 1, |#Q| - 1) o <#E> and
                E = Element_At(|#Q|, #Q); 

    Operation Length(restores Q: P_Queue): Integer;
        ensures Length = (|Q|);
 
    Operation Rem_Capacity(restores Q: P_Queue): Integer;
        ensures Rem_Capacity = (Max_Length - |Q|);
 
    Operation Clear(clears Q: P_Queue);
 
end Preemptable_Queue_Template;
