Concept Sequence_Template(type Entry; evaluates Max_Length: Integer);
    uses Std_Integer_Fac, String_Theory;
    requires Max_Length > 0;
 
    Type Family Sequence is modeled by Str(Entry);
    exemplar S;
    constraint |S| <= Max_Length;
    initialization ensures S = Empty_String;
 
    Operation Insert_after(evaluates pos: Integer; alters E: Entry; 
		updates S: Sequence);
        requires 0 <= pos and pos <= |S| and |S| < Max_Length;
        ensures S = Substring(#S, 1, pos) o <#E> o 
		Substring(#S, pos + 1, |#S| - pos);
 
    Operation Remove_after(evaluates pos: Integer; replaces R: Entry; 
		updates S: Sequence);
        requires 0 <= pos and pos < |S| and |S| > 0;
        ensures S = Substring(#S, 1, pos) o Substring(#S, pos + 2, |#S| - pos - 1) 
		and R = Element_At(pos + 1, #S);

    Operation Length(restores S: Sequence): Integer;
        ensures Length = (|S|);
 
    Operation Rem_Capacity(restores S: Sequence): Integer;
        ensures Rem_Capacity = (Max_Length - |S|);
 
    Operation Clear(clears S: Sequence);
 
end Sequence_Template;
